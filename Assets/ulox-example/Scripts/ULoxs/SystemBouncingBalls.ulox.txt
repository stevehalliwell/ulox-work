/*
Code we expect from Unity's side;
SetUIText(string)
CreateFromPrefab(string):GameObject (returns a GameObject reference that we'll pass back to unity)
GetKey(string):bool
SetGameObjectPosition(GameObject, x, y, z)
ReloadScene - reload the current unity scene


Unity will find and call: 
- SetupGame in Start
- Update in Update
*/

//functions and data
var dt = 0;
var limit = 5;
var numBallsToSpawn = 100;

var ballName = "BouncyBall";
var balls = List();

class Ball
{
	var x = RandRange(-3,3);
	var y = RandRange(-3,3);
	var vx = RandRange(-3,3);
	var vy = RandRange(-3,3);
	var go;

	init(self) 
	{
		self.go = CreateFromPrefab(ballName);
	}

	Tick(self)
	{
		self.x += self.vx * dt;
		self.y += self.vy * dt;

		//bounds
		if(self.x < -limit and self.vx < 0) { self.vx *= -1; }
		if(self.x > limit  and self.vx > 0) { self.vx *= -1; }
		if(self.y < -limit and self.vy < 0) { self.vy *= -1; }
		if(self.y > limit  and self.vy > 0) { self.vy *= -1; }

		SetGameObjectPosition(self.go, self.x, self.y, 0);
	}
}

fun SetupGame()
{
	print ("Setting Up Game");

	for(var i = 0; i < numBallsToSpawn; i += 1)
	{
		balls.Add(Ball());
	}
}

fun Update()
{
	if(GetKey("escape")){ReloadScene();}

	var len = balls.Count();
	for(var i = 0; i < len; i += 1)
	{
		Ball.Tick(balls.Get(i));
	}
}
