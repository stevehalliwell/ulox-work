/*
Code we expect from Unity's side;
SetUIText(string)
CreateFromPrefab(string):GameObject (returns a GameObject reference that we'll pass back to unity)
GetKey(string):bool
SetGameObjectPosition(GameObject, x, y)
ReloadScene - reload the current unity scene


Unity will find and call: 
- SetupGame in Start
- Update in Update
*/

//functions and data
var dt = 0;
var limit = 5;
var numBallsToSpawn = 100;

var ballName = "BouncyBall";
var balls = List();

class Ball
{
	var x = RandRange(-3,3);
	var y = RandRange(-3,3);
	var vx = RandRange(-3,3);
	var vy = RandRange(-3,3);
	var go;

	init() 
	{
		go = CreateFromPrefab(ballName);
	}

	Tick()
	{
		x += vx * dt;
		y += vy * dt;

		//bounds
		if(x < -limit and vx < 0) { vx *= -1; }
		if(x > limit  and vx > 0) { vx *= -1; }
		if(y < -limit and vy < 0) { vy *= -1; }
		if(y > limit  and vy > 0) { vy *= -1; }

		SetGameObjectPosition(go, x, y);
	}
}

fun SetupGame()
{
	print ("Setting Up Game");

	for(var i = 0; i < numBallsToSpawn; i += 1)
	{
		balls.Add(Ball());
	}
}

fun Update()
{
	if(GetKey("escape")){ReloadScene();}

	var len = balls.Count();
	for(var i = 0; i < len; i += 1)
	{
		balls.Get(i).Tick();
	}
}
